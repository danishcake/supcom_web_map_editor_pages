<html>
  <head>
    <title>Supreme Commander Map Editor</title>
    <link rel="stylesheet" type="text/css" href="app.css"/>
  </head>
  <body ng-app="sc_map_edit_bin">
    <top-menu ng-controller="top-menu"></top-menu>
    <editor-menu ng-controller="editor-menu"></editor-menu>
    <editor-view ng-controller="editor-view"></editor-view>
  </body>

  <script src="app.bundle.js"></script>

  <!-- Shaders for rendering simple greyscale heightmap -->
  <script id="vs-terrain-greyscale" type="x-shader/x-vertex">
    precision highp float;
    attribute vec3 aVertexPosition;
    varying highp float vHeight;
    varying lowp float vGradient;
    uniform mat4 uVMatrix;
    uniform mat4 uPMatrix;
    uniform vec2 uMapSize;
    uniform highp sampler2D uHeightmap;
    uniform highp float uHeightmapScale;
    uniform highp float uShadeMin;
    uniform highp float uShadeMax;

    void main(void) {
      vec2 textureCoord = aVertexPosition.xy / uMapSize;
      highp float height = texture2D(uHeightmap, textureCoord).a;
      vec3 displacedPosition = aVertexPosition + vec3(0, 0, height * uHeightmapScale);
      gl_Position = uPMatrix * uVMatrix * vec4(displacedPosition, 1.0);
      vHeight = (height - uShadeMin) / (uShadeMax - uShadeMin);

      // Calculate the gradient
      highp vec2 delta      = vec2(1.0, 1.0) / uMapSize;
      highp float left      = texture2D(uHeightmap, textureCoord.xy + vec2( delta.x, 0)).a;
      highp float right     = texture2D(uHeightmap, textureCoord.xy + vec2(-delta.x, 0)).a;
      highp float up        = texture2D(uHeightmap, textureCoord.xy + vec2(0,        delta.y)).a;
      highp float down      = texture2D(uHeightmap, textureCoord.xy + vec2(0,        -delta.y)).a;
      vGradient = max(max(abs(left - height), abs(right - height)),
                      max(abs(up   - height), abs(down  - height)));
    }
  </script>


  <script id="fs-terrain-greyscale" type="x-shader/x-fragment">
    precision mediump float;
    varying highp float vHeight;
    varying lowp float vGradient;
    uniform mediump float uNavigabilityThreshold;
    void main(void) {
      vec4 op = vec4(vHeight, vHeight, vHeight, 1.0);
      // The navigability overlay is disabled by setting the threshold very high
      op.r += step(uNavigabilityThreshold, vGradient);
      gl_FragColor = op;
    }
  </script>


  <!-- Shaders for rendering a textured heightmap -->
  <script id="vs-terrain-textured" type="x-shader/x-vertex">
    precision highp float;
    attribute vec3 aVertexPosition;
    uniform mat4 uVMatrix;
    uniform mat4 uPMatrix;
    uniform vec2 uMapSize;
    uniform highp sampler2D uHeightmap;
    uniform highp float uHeightmapScale;
    varying highp vec2 vTextureCoord;
    varying lowp float vGradient;

    void main(void) {
      vTextureCoord = aVertexPosition.xy / uMapSize;
      highp float height = texture2D(uHeightmap, vTextureCoord).a;
      vec3 displacedPosition = aVertexPosition + vec3(0, 0, height * uHeightmapScale);
      gl_Position = uPMatrix * uVMatrix * vec4(displacedPosition, 1.0);

      // Calculate the gradient
      highp vec2 delta      = vec2(1.0, 1.0) / uMapSize;
      highp float left      = texture2D(uHeightmap, vTextureCoord.xy + vec2( delta.x, 0)).a;
      highp float right     = texture2D(uHeightmap, vTextureCoord.xy + vec2(-delta.x, 0)).a;
      highp float up        = texture2D(uHeightmap, vTextureCoord.xy + vec2(0,        delta.y)).a;
      highp float down      = texture2D(uHeightmap, vTextureCoord.xy + vec2(0,        -delta.y)).a;
      vGradient = max(max(abs(left - height), abs(right - height)),
                      max(abs(up   - height), abs(down  - height)));
    }
  </script>


  <script id="fs-terrain-textured" type="x-shader/x-fragment">
    precision mediump float;
    varying highp vec2 vTextureCoord;
    varying lowp float vGradient;
    uniform mediump sampler2D uTerrainChan03;
    uniform mediump sampler2D uTerrainChan47;
    uniform lowp sampler2D uTerrainTextureBase;
    uniform lowp sampler2D uTerrainTexture0;
    uniform lowp sampler2D uTerrainTexture1;
    uniform lowp sampler2D uTerrainTexture2;
    uniform lowp sampler2D uTerrainTexture3;
    uniform lowp sampler2D uTerrainTexture4;
    uniform lowp sampler2D uTerrainTexture5;
    uniform lowp sampler2D uTerrainTexture6;
    uniform lowp sampler2D uTerrainTexture7;
    uniform lowp sampler2D uTerrainTextureMacro;
    uniform mediump float uTextureScaleBase;
    uniform mediump float uTextureScale0;
    uniform mediump float uTextureScale1;
    uniform mediump float uTextureScale2;
    uniform mediump float uTextureScale3;
    uniform mediump float uTextureScale4;
    uniform mediump float uTextureScale5;
    uniform mediump float uTextureScale6;
    uniform mediump float uTextureScale7;
    uniform mediump float uTextureScaleMacro;
    uniform mediump float uNavigabilityThreshold;
    // TODO: Minimum 0.5 threshold
    // This comes from terrain.fx. Mask here is the uTerrainChan03 variable
    // float4 mask = saturate(tex2Dproj( UtilitySamplerA, inV.mTexWT  * TerrainScale)* 2 - 1 );
    // This occurs whenever the chan03/47 variables are used for texturing, but not when working with normals
    // Here the full range is used to blend the normal maps together in a similar fashion.
    // So value below 0.5 will have no effect on the colour of the terrain, but WILL start affecting the normal output
    // eg 0.4 will cause that normal channel to be weighted at 40%
    // So how do I deal with this? I don't intend to (initially) handle normals, so changes below 0.5 will
    // be invisible. Instead I'll try to keep the range of values between 0.5 and 1

    // Notes from terrain.fx
    // Textures are scale according to textureCoord * TerrainScale * PerStrataScale
    // There isn't much of an indication of what the value of TerrainScale is, but the PerStrataScale
    // is referred to as 'LowerAlbedoTile' etc, so it could reasonable be expected to scale the texture coordinates
    // into the range 0-1, which I'm already doing

    void main(void) {
      vec4 chan03 = texture2D(uTerrainChan03, vTextureCoord);
      vec4 chan47 = texture2D(uTerrainChan47, vTextureCoord);

      // TODO: Figure out how to blend this properly, overwriting the previous value
      // TBD: Is there a lerp primative that is quicker here?
      vec4 op = texture2D(uTerrainTextureBase, vTextureCoord * uTextureScaleBase);
      op = mix(op, texture2D(uTerrainTexture0, vTextureCoord * uTextureScale0), clamp(chan03.r * 2.0 - 1.0, 0.0, 1.0));
      op = mix(op, texture2D(uTerrainTexture1, vTextureCoord * uTextureScale1), clamp(chan03.g * 2.0 - 1.0, 0.0, 1.0));
      op = mix(op, texture2D(uTerrainTexture2, vTextureCoord * uTextureScale2), clamp(chan03.b * 2.0 - 1.0, 0.0, 1.0));
      op = mix(op, texture2D(uTerrainTexture3, vTextureCoord * uTextureScale3), clamp(chan03.a * 2.0 - 1.0, 0.0, 1.0));
      op = mix(op, texture2D(uTerrainTexture4, vTextureCoord * uTextureScale4), clamp(chan47.r * 2.0 - 1.0, 0.0, 1.0));
      op = mix(op, texture2D(uTerrainTexture5, vTextureCoord * uTextureScale5), clamp(chan47.g * 2.0 - 1.0, 0.0, 1.0));
      op = mix(op, texture2D(uTerrainTexture6, vTextureCoord * uTextureScale6), clamp(chan47.b * 2.0 - 1.0, 0.0, 1.0));
      op = mix(op, texture2D(uTerrainTexture7, vTextureCoord * uTextureScale7), clamp(chan47.a * 2.0 - 1.0, 0.0, 1.0));
      // Last channel is mixed in using the alpha channel, but I must have screwed up the conversion as it
      // appears the alpha channel is fully set to 255.
      // For now I'm just going to mix this in at 10%
      op = mix(op, texture2D(uTerrainTextureMacro, vTextureCoord * uTextureScaleMacro), 0.1);

      // The navigability overlay is disabled by setting the threshold very high
      op.r += step(uNavigabilityThreshold, vGradient);
      gl_FragColor = op;
    }
  </script>


  <!-- Shaders for rendering icons on the map -->
  <script id="vs-marker" type="x-shader/x-vertex">
    precision highp float;
    varying vec2 vTextureCoordinate;
    attribute vec3 aVertexPosition;
    attribute vec2 aTextureCoordinate;
    uniform mat4 uMMatrix;
    uniform mat4 uVMatrix;
    uniform mat4 uPMatrix;

    void main(void) {
      gl_Position = uPMatrix * uVMatrix * uMMatrix * vec4(aVertexPosition, 1.0);
      vTextureCoordinate = aTextureCoordinate;
    }
  </script>

  <script id="fs-marker" type="x-shader/x-fragment">
    precision mediump float;
    varying vec2 vTextureCoordinate;
    uniform highp sampler2D uMarkerTexture;
    uniform vec3 uTint;
    void main(void) {
      vec4 textureColor = texture2D(uMarkerTexture, vTextureCoordinate);
      gl_FragColor = textureColor + vec4(uTint, 0);
    }
  </script>

  <!-- Shaders for rendering coloured lines -->
  <script id="vs-line" type="x-shader/x-vertex">
    precision highp float;
    attribute vec3 aVertexPosition;
    uniform mat4 uMMatrix;
    uniform mat4 uVMatrix;
    uniform mat4 uPMatrix;

    void main(void) {
      gl_Position = uPMatrix * uVMatrix * uMMatrix * vec4(aVertexPosition, 1.0);
    }
  </script>

  <script id="fs-line" type="x-shader/x-fragment">
    precision mediump float;
    uniform vec4 uLineColour;
    void main(void) {
      gl_FragColor = uLineColour;
    }
  </script>

  <!-- Shaders for rendering water -->
  <script id="vs-water" type="x-shader/x-vertex">
    precision highp float;
    attribute vec3 aVertexPosition;
    uniform mat4 uMMatrix;
    uniform mat4 uVMatrix;
    uniform mat4 uPMatrix;

    void main(void) {
      gl_Position = uPMatrix * uVMatrix * uMMatrix * vec4(aVertexPosition, 1.0);
    }
  </script>

  <script id="fs-water" type="x-shader/x-fragment">
    precision mediump float;
    uniform vec4 uWaterColour;
    void main(void) {
      gl_FragColor = uWaterColour;
    }
  </script>

  <!-- Shaders for rendering line above terrain -->
  <script id="vs-line-above-terrain" type="x-shader/x-vertex">
    precision highp float;
    attribute vec2 aVertexPosition;
    uniform mat4 uMMatrix;
    uniform mat4 uVMatrix;
    uniform mat4 uPMatrix;
    uniform vec2 uMapSize;
    uniform highp sampler2D uHeightmap;
    uniform highp float uHeightmapScale;

    void main(void) {
      vec2 textureCoord = aVertexPosition.xy / uMapSize;
      highp float height = texture2D(uHeightmap, textureCoord).a;

      gl_Position = uPMatrix * uVMatrix * uMMatrix * vec4(aVertexPosition, height * uHeightmapScale, 1.0);
    }
  </script>
</html>
